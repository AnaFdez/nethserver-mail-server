#!/usr/bin/perl

#
# nethserver-mail-account-update
#
# Set / Unset a custom mail quota for specific user(s) and group(s)
#

#
# Copyright (C) 2012 Nethesis S.r.l.
# http://www.nethesis.it - support@nethesis.it
# 
# This script is part of NethServer.
# 
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
# 
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see <http://www.gnu.org/licenses/>.
#

use esmith::AccountsDB;
use NethServer::Directory;
use NethServer::MailServer;
use strict;

my $event = shift || die("Missing event name argument");
my $account = shift;

my $accountsDb = esmith::AccountsDB->open_ro();

my @accountList = ();

my $errors = 0;

if( ! $account ) {
    @accountList = ($accountsDb->users(), $accountsDb->groups());
} else {
    @accountList = ($accountsDb->get($account));
}

my $ldap = NethServer::Directory->new() || die("LDAP connection error");

foreach my $accountRecord (@accountList) {

    if($accountRecord->prop('type') ne 'user'
	&& $accountRecord->prop('type') ne 'group') {
	warn "Skipping invalid account type:" . $accountRecord->key . "\n";
	next;
    }

    my $ldapEntry = $ldap->getUser($accountRecord->key);

    if( ! defined $ldapEntry ) {
	warn("Account `" . $accountRecord->key . "` has not been found!\n");
	next;
    }

    my $mailServer = NethServer::MailServer->new();

    my @accountPseudonyms = $mailServer->getAccountPseudonyms($accountRecord->key);

    if( ! grep(/^qmailUser$/, $ldapEntry->get_value('objectClass')))  {
	$ldapEntry->add(objectClass => 'qmailUser', mail => '');
	$ldapEntry->update($ldap);
    }

    if(@accountPseudonyms) {
	$ldapEntry->replace(mail => \@accountPseudonyms);
    } else {
	$ldapEntry->replace(mail => '');
    }
    
    my $isEnabledMailStatus = defined $accountRecord->prop('MailStatus')
	&& $accountRecord->prop('MailStatus') eq 'enabled';
    
    # See accountStatus attributeType in qmail schema:
    if ( $isEnabledMailStatus ) {
	$ldapEntry->replace(accountStatus => 'active');
    } else {
	$ldapEntry->replace(accountStatus => 'disabled');
    }

    if( ! defined $accountRecord->prop('MailQuotaType') 
	|| $accountRecord->prop('MailQuotaType') ne 'custom') {
	if($ldapEntry->exists('mailQuotaSize')) {
	    $ldapEntry->delete('mailQuotaSize');
	}
    } elsif ($accountRecord->prop('MailQuotaCustom') eq 'unlimited') {
	$ldapEntry->replace(mailQuotaSize => 0);
    } else {
	$ldapEntry->replace(mailQuotaSize => $accountRecord->prop('MailQuotaCustom') * 100);
    }

    $ldapEntry->update($ldap);
}


exit($errors == 0 ? 0 : 1);
