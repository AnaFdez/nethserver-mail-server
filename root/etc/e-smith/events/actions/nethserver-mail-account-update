#!/usr/bin/perl

#
# nethserver-mail-account-update
#
# Set / Unset a custom mail quota for specific user(s) and group(s)
#

use esmith::AccountsDB;
use NethServer::Directory;
use strict;

my $event = shift || die("Missing event name argument");
my $account = shift;

my $accountsDb = esmith::AccountsDB->open_ro();

my @accountList = ();

my $errors = 0;

if( ! $account ) {
    @accountList = ($accountsDb->users(), $accountsDb->groups());
} else {
    @accountList = ($accountsDb->get($account));
}

my $ldap = NethServer::Directory::connect() || die("LDAP connection error");


foreach my $accountRecord (@accountList) {

    if($accountRecord->prop('type') ne 'user'
	&& $accountRecord->prop('type') ne 'group') {
	warn "Skipping invalid account type:" . $accountRecord->key . "\n";
	next;
    }

    my $ldapEntry = $ldap->search(
	base => 'ou=People, ' . NethServer::Directory::getInternalSuffix(),
	scope => 'one',
	filter => sprintf('(&(objectClass=PosixAccount)(uid=%s))', $accountRecord->key),
	)->pop_entry();

    if( ! defined $ldapEntry ) {
	warn("Account `" . $accountRecord->key . "` has not been found!\n");
	next;
    }

    if( ! grep(/^qmailUser$/, $ldapEntry->get_value('objectClass')))  {
	$ldapEntry->add(objectClass => 'qmailUser', mail => '');
	$ldapEntry->update($ldap);
    }

    my $isEnabledMailStatus = defined $accountRecord->prop('MailStatus')
	&& $accountRecord->prop('MailStatus') eq 'enabled';
    
    my $isSharedGroup = $accountRecord->prop('type') eq 'group'
	&& $accountRecord->prop('MailDeliveryType') eq 'shared';

    # See accountStatus attributeType in qmail schema:
    if ( $isEnabledMailStatus &&
	( $accountRecord->prop('type') eq 'user' || $isSharedGroup )) {
	$ldapEntry->replace(accountStatus => 'active');
    } else {
	$ldapEntry->replace(accountStatus => 'disabled');
    }

    if( ! defined $accountRecord->prop('MailQuotaType') 
	|| $accountRecord->prop('MailQuotaType') ne 'custom') {
	if($ldapEntry->exists('mailQuotaSize')) {
	    $ldapEntry->delete('mailQuotaSize');
	}
    } elsif ($accountRecord->prop('MailQuotaCustom') eq 'unlimited') {
	$ldapEntry->replace(mailQuotaSize => 0);
    } else {
	$ldapEntry->replace(mailQuotaSize => $accountRecord->prop('MailQuotaCustom') * 100);
    }

    $ldapEntry->update($ldap);
}


exit($errors == 0 ? 0 : 1);
