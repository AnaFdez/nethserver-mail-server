#!/usr/bin/perl

#
# nethserver-mail-group-acl-adjust
#
# Keep IMAP ACL updated for group accounts. The optional $accountArg
# limit the operation on the given group account only.
#

use strict;
use esmith::AccountsDB;
use NethServer::MailServer;


my $accountsDb = esmith::AccountsDB->open_ro();

my $event = shift || die("Missing event argument");
my $accountArg = shift;

my @accounts = ();
my $errors = 0;

if($accountArg) {
    @accounts = @{[$accountsDb->get($accountArg)]};
} else {
    @accounts = $accountsDb->groups();
}

my $vmailGid = (getgrnam('vmail'))[2] or die('Missing vmail group');
my $vmailUid = (getpwnam('vmail'))[2] or die('Missing vmail user'); 

foreach my $accountRecord (@accounts) {

    my $account = $accountRecord->key;
    my $retval;

    if($accountRecord->prop('type') ne 'group') {
	die("Account `$account` is not a group");
    }

    # Skip group if mail is disabled. Fixes #1560
    if($accountRecord->prop('MailStatus') ne 'enabled') {
	next;
    }

    my $imap = NethServer::MailServer->connectAclManager($account . '*vmail');
	    
    if( ! $imap) {
	die("Could not create connection to IMAP server. Account: `$account`.");
    }
        
    my $dovecotSharedPath = NethServer::MailServer::getDovecotSharedPath($account);

    #
    # Set/Unset group ($) ACL to share the INBOX folder    
    #
    my $deliveryType = $accountRecord->prop('MailDeliveryType');
    if($deliveryType eq 'copy' || $accountRecord->prop('MailStatus') eq 'disabled') {
	$retval = $imap->deleteAcl('INBOX', '$' . $account);
	unlink($dovecotSharedPath);
    } elsif($deliveryType eq 'shared') {
	$retval = $imap->setAcl('INBOX', '$' . $account, 'lrs');
	open(T, '>', $dovecotSharedPath) && close(T) && chown $vmailUid, $vmailGid, $dovecotSharedPath;
    } else {
	warn("Skipping unknown delivery type: $deliveryType\n");
	$retval = 1;
    }

    if( ! $retval) {
	warn("IMAP error for account $account: " . $imap->getErrorMessage());
	$errors ++;
    }

    $imap->logout();
}


exit ($errors == 0 ? 0 : 1);
